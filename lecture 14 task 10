#include <stdint.h>

// Encrypted ciphertext bytes
const uint8_t cipher[] = {
  87, 26, 72, 13, 67,
  95, 89, 23, 72, 22,
  73, 11, 87, 30, 73
};
const uint8_t cipherLen = sizeof(cipher); // Ciphertext length

char plaintext[cipherLen + 1]; // Buffer for decrypted text

// Checks if the plaintext contains two lowercase words with one space
bool isTwoLowercaseWords(const char *s) {
  int spaces = 0;

  for (uint8_t i = 0; i < cipherLen; i++) {
    char c = s[i];

    if (c == ' ') {
      spaces++;
    }
    else if (c < 'a' || c > 'z') {
      return false; // Rejects non-lowercase chars
    }
  }
  
  if (spaces != 1 || s[0] == ' ' || s[cipherLen - 1] == ' ') return false; // Ensures correct format
  return true;
}

void setup() {
  Serial.begin(9600); // Serial communication
  while (!Serial) { ; }

  Serial.println(F("Brute-forcing two XOR keys...\n"));

  unsigned long startTotal = millis(); // Start time

  // Try all 256 x 256 key combinations
  for (uint16_t key0 = 0; key0 < 256; key0++) {
    for (uint16_t key1 = 0; key1 < 256; key1++) {

      // Decrypt with current key pair
      for (uint8_t i = 0; i < cipherLen; i++) {
        uint8_t useKey = (i & 1) ? key1 : key0; // Key selection
        plaintext[i] = (char)(cipher[i] ^ useKey);
      }
      plaintext[cipherLen] = '\0'; // Null-terminate string

      if (isTwoLowercaseWords(plaintext)) {
        unsigned long elapsed = millis() - startTotal; // Time taken for match

        Serial.println(F("POSSIBLE MATCH FOUND"));
        Serial.print(F("key0 = "));
        Serial.print(key0);
        Serial.print(F(" (0x"));
        Serial.print(key0, HEX);
        Serial.println(F(")"));

        Serial.print(F("key1 = "));
        Serial.print(key1);
        Serial.print(F(" (0x"));
        Serial.print(key1, HEX);
        Serial.println(F(")"));

        Serial.print(F("Plaintext: \""));
        Serial.print(plaintext);
        Serial.println(F("\""));

        Serial.print(F("Time taken (ms): "));
        Serial.println(elapsed);
        Serial.println(F("\n"));
      }
    }
  }

  unsigned long totalElapsed = millis() - startTotal; // Total time

  Serial.println(F("Brute force finished."));
  Serial.print(F("TOTAL TIME (ms): "));
  Serial.println(totalElapsed);
}

void loop() {
  // Empty
}
